requires BEAST.base;
requires feast;
requires BEASTLabs;


// use feast
Alignment alignment_data = AlignmentFromNexus(fileName="RSV2.nex");
@data
Alignment codon1 = FilteredAlignment(data=alignment_data, filter="3-629\3");
@data
Alignment codon2 = FilteredAlignment(data=alignment_data, filter="1-629\3");
@data
Alignment codon3 = FilteredAlignment(data=alignment_data, filter="2-629\3");

// the earliest AUSA2s61=41.0
TraitSet traitSet = TraitSetFromTaxonSet(delimiter="s", everythingAfterLast="true",
                            traitname="date", dateFormat="yy", taxa=alignment_data);

// unlink subst models
RealParameter kappa1 ~ LogNormalDistributionModel(M=1, S=0.5);
RealParameter freqs1 ~ Dirichlet(alpha=[2.0, 2.0, 2.0, 2.0]);
HKY hky1 = HKY(kappa=kappa1, frequencies=freqs1);

RealParameter kappa2 ~ LogNormalDistributionModel(M=1, S=0.5);
RealParameter freqs2 ~ Dirichlet(alpha=[2.0, 2.0, 2.0, 2.0]);
HKY hky2 = HKY(kappa=kappa2, frequencies=freqs2);

RealParameter kappa3 ~ LogNormalDistributionModel(M=1, S=0.5);
RealParameter freqs3 ~ Dirichlet(alpha=[2.0, 2.0, 2.0, 2.0]);
HKY hky3 = HKY(kappa=kappa3, frequencies=freqs3);

// Fix mean of 3 relative rates to 1
RealParameter r ~ WeightedDirichlet(alpha=[1.0, 1.0, 1.0], weights=[209, 210, 210]);
Function r_1 = Slice(arg=r, index=0);
Function r_2 = Slice(arg=r, index=1);
Function r_3 = Slice(arg=r, index=2);

SiteModelGI siteModel1 = SiteModelGI(substModel=hky1, mutationRate=r_1);
SiteModelGI siteModel2 = SiteModelGI(substModel=hky2, mutationRate=r_2);
SiteModelGI siteModel3 = SiteModelGI(substModel=hky3, mutationRate=r_3);

// link tree
RealParameter theta ~ LogNormalDistributionModel(M=3, S=2);
PopulationFunction popFunc = ConstantPopulation(popSize=theta);
// trait= takes 2nd order inputs
Tree tree ~ Coalescent(populationModel=popFunc, taxonset=alignment_data, trait=traitSet);

RealParameter clockRate ~ LogNormalDistributionModel(M=-5, S=1.25);
StrictClockModel clockModel = StrictClockModel(clock.rate=clockRate);

@observed(data=codon1)
Alignment codon1 ~ TreeLikelihood(siteModel=siteModel1, tree=tree, branchRateModel=clockModel);
@observed(data=codon2)
Alignment codon2 ~ TreeLikelihood(siteModel=siteModel2, tree=tree, branchRateModel=clockModel);
@observed(data=codon3)
Alignment codon3 ~ TreeLikelihood(siteModel=siteModel3, tree=tree, branchRateModel=clockModel);
